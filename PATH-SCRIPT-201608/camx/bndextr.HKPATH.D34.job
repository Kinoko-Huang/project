#!/bin/csh -f
#
#  BNDEXTR can be used to set up 1-way nesting with CAMx
# 
#  Reads a coarse grid CAMx average file and:
#  - extracts concentrations for a user-specified grid nest
#  - writes a boundary condition file for the grid nest
#  - optionally writes an initial condition file for the grid nest
#  - writes diagnostic information on boundary file concentrations
#
#  The nested grid is located by iwest,ieast,jsouth,jnorth
#  which have the same meaning as nest parameters in the
#  CAMx.in file. They are the coarse grid cell numbers containing
#  the lower left (iwest,jsouth) and upper right (ieast,jnorth)
#  corners of the grid nest, excluding buffer cells.  BNDEXTR
#  adds buffer cells to the grid auromatically.  The meshing
#  factor is an integer ratio of the nested grid spacing 
#  relative to the coarse grid spacing.  

source ../../../domains_def.config

set BEG_JDATE = $argv[2] #The initial day for this simulation
set CUR_JDATE = $argv[1] #Current simulation day
set DOMAINS_GRID = $argv[3]
set DOMAINS_RES = $argv[4]

@ GRID_COARSE = $DOMAINS_GRID - 1
set CAMxDir = $CAMx_HOME/inputs/icbc

echo $CUR_JDATE $BEG_JDATE 'BCfile'

rm -f msg/camx.${DOMAINS_RES}km.*.diag1
rm -f msg/camx.${DOMAINS_RES}km.*.diag2
rm -f msg/log.bndexr.${DOMAINS_RES}.*
rm -f $CAMxDir/bc.camx.${DOMAINS_RES}km.$CUR_JDATE.uam
rm -f $CAMxDir/icon.camx.${DOMAINS_RES}km.$BEG_JDATE.bin

set ic = "F"
if ( $CUR_JDATE == $BEG_JDATE ) then
  set ic = "T"
endif

set CUR_Y = `$CAMx_HOME/datelib/j2g $CUR_JDATE | awk '{print $1}'`
set CUR_M   = `$CAMx_HOME/datelib/j2g $CUR_JDATE | awk '{print $2}'`
set CUR_D   = `$CAMx_HOME/datelib/j2g $CUR_JDATE | awk '{print $3}'`
set CUR_J   = `echo $CUR_JDATE |cut -c 3-7`
set CUR_GDATE = $CUR_Y$CUR_M$CUR_D

set iwest = ${IWEST_AQMS_Dx[$DOMAINS_GRID]}
set ieast = ${IEAST_AQMS_Dx[$DOMAINS_GRID]}
set jsouth = ${JSOUTH_AQMS_Dx[$DOMAINS_GRID]}
set jnorth = ${JNORTH_AQMS_Dx[$DOMAINS_GRID]}
set RUN = HKPATH.${G_DOMAINS_RES_AQMS[$GRID_COARSE]}
ln -fs $CAMxOUT/$RUN/CAMx5.4.$RUN.$CUR_GDATE.avrg.grd01 CAMx5.4.$RUN.$CUR_GDATE.avrg

./bndextr.linux << EOF >& ./msg/log.bndexr.${DOMAINS_RES}.$CUR_JDATE
input average file |CAMx5.4.$RUN.$CUR_GDATE.avrg
output BC file     |$CAMxDir/bc.camx.${DOMAINS_RES}km.$CUR_JDATE.uam
iwst,iest,jsth,jnth|$iwest,$ieast,$jsouth,$jnorth
mesh factor        |3
make IC file?      |$ic
time/date for IC   |${BEG_H}  $CUR_J
output IC file     |$CAMxDir/icon.camx.${DOMAINS_RES}km.${BEG_JDATE}.bin
diagnostic file #1 |msg/camx.${DOMAINS_RES}km.$CUR_JDATE.diag1
diagnostic file #2 |msg/camx.${DOMAINS_RES}km.$CUR_JDATE.diag2
EOF


rm -f CAMx5.4.$RUN.*
