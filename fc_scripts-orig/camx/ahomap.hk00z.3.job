#!/bin/csh -f 
#
# User input data definition
#
# Lines 1 & 2: Coordiniate projection and master/coarse grid location definition
# Coordinate project |UTM
# xorg,yorg,izone    |-316.,2808.,15                (km,km,unitless)
# or
# Coordinate project |LAMBERT
# xorg,yrog,clon,clat|-2000,-1700,-100,40,60,30     (km,km,deg,deg,deg,deg)
# true1,true2
# or
# Coordinate project |LATLON
# xorg,yorg          |30,-120                       (deg,deg)
# or 
# Coordinate project |POLAR
# xorg,yorg,plon,plat|-100,-200,-100,40             (km,km,deg,deg)
#
# Line 3: Master/coarse grid cell size (km)
# dx,dy              |16,16
#
# Line 4: Output file name
# Output filename    |ahomap.test
#
# Line 5: Snow flag (T=5th albedo bin set for snow, F=no snow albedo included)
# Process for snow?  |.true.
#
# Line 6: Number of grids to process
# Number of grids    |1
#
# Lines 7 and 8: Landuse filename and domain size (number of grid cells)
#                List the master/coarse grid first, then any/all fine grids
# Landuse file       |camx.landuse
# nx, ny             |95,83
#
# Line 9: Number of TOMS input data files
# Number of TOMS file|28
#
# Lines 10+: Begin date, End date,and the Ozone data file name.
#            Requires begin date (YYMMDD) and end date (YYMMDD) for ozone 
#            column data.
#
# Example: for August 15, 1993 daily ozone data, 
# Bday,Eday,TOMS file|930815,930815,ozcol_data/ga930815.m3t
#
# Example: for August 1993 monthly ozone data,
# Bday,Eday,TOMS file|930801,930831,ozcol_data/m3_aug93.mon
#

set BDATE = $argv[1]
set EDATE = $argv[2]
set JDATE = $BDATE

rm -f ../msg/log.ahomap.*.3km

while ( $JDATE <= $EDATE )
set YYYY = `$CAMx_HOME/datelib/j2g $JDATE | awk '{print $1}'`
set MM   = `$CAMx_HOME/datelib/j2g $JDATE | awk '{print $2}'`
set DD   = `$CAMx_HOME/datelib/j2g $JDATE | awk '{print $3}'`
set YY   = `echo $YYYY | cut -c 3-4`
set DD1 = ` expr "$DD" \+ "1"`

ln -sf $CAMx_HOME/inputs/met/3/camx_lu.$YYYY$MM$DD.3km.bin camx_lu.$YYYY$MM$DD.3km.bin
ln -sf $CAMx_HOME/Raw_DATA/TOMs/L3_ozone_omi_$YYYY$MM$DD.txt L3_ozone_omi_$YYYY$MM$DD.txt


$cwd/../src/ahomap.linux << EOF >& ../msg/log.ahomap.$JDATE.3km
Coordinate project |LAMBERT
xorg,yorg,izone    |-291.,-754.5,114,28.5,15,40
dx,dy              |3.,3.
Output filename    |ahomap.hk.${YYYY}${MM}${DD}.3km
Process for snow?  |.false.
Number of grids    |1
Landuse filename   |camx_lu.$YYYY$MM$DD.3km.bin
nx,ny              |152,110
Numbr of TOMS files|1
Bday,Eday,TOMS file|$YY$MM$DD,$YY$MM$DD,L3_ozone_omi_$YYYY$MM$DD.txt
EOF

rm -f camx_lu.$YYYY$MM$DD.3km.bin
rm -f L3_ozone_omi_$YYYY$MM$DD.txt

mv ahomap.hk.${YYYY}${MM}${DD}.3km $CAMx_HOME/inputs/ahomap

set JDATE = `$CAMx_HOME/datelib/yyyyjjj_plus_dd $JDATE 1`

end

