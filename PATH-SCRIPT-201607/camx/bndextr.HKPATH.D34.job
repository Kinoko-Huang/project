#!/bin/csh -f
#
#  BNDEXTR can be used to set up 1-way nesting with CAMx
# 
#  Reads a coarse grid CAMx average file and:
#  - extracts concentrations for a user-specified grid nest
#  - writes a boundary condition file for the grid nest
#  - optionally writes an initial condition file for the grid nest
#  - writes diagnostic information on boundary file concentrations
#
#  The nested grid is located by iwest,ieast,jsouth,jnorth
#  which have the same meaning as nest parameters in the
#  CAMx.in file. They are the coarse grid cell numbers containing
#  the lower left (iwest,jsouth) and upper right (ieast,jnorth)
#  corners of the grid nest, excluding buffer cells.  BNDEXTR
#  adds buffer cells to the grid auromatically.  The meshing
#  factor is an integer ratio of the nested grid spacing 
#  relative to the coarse grid spacing.  

source ../../../domains_def.config

set BDATE = $argv[2] #The initial day for this simulation
set JDATE = $argv[1] #Current simulation day
set GRID = $argv[3]
set DOMAINS_RES = $argv[4]

@ GRID_COARSE = $GRID - 1
set CAMxDir = $CAMx_HOME/inputs/icbc

echo $JDATE $BDATE 'BCfile'

rm -f msg/camx.${DOMAINS_RES}km.*.diag1
rm -f msg/camx.${DOMAINS_RES}km.*.diag2
rm -f msg/log.bndexr.${DOMAINS_RES}.*
rm -f $CAMxDir/bc.camx.${DOMAINS_RES}km.$JDATE.uam
rm -f $CAMxDir/icon.camx.${DOMAINS_RES}km.$BDATE.bin

set ic = "F"
if ( $JDATE == $BDATE ) then
  set ic = "T"
endif

set YYYY = `$CAMx_HOME/datelib/j2g $JDATE | awk '{print $1}'`
set MM   = `$CAMx_HOME/datelib/j2g $JDATE | awk '{print $2}'`
set DD   = `$CAMx_HOME/datelib/j2g $JDATE | awk '{print $3}'`
set dy   = `echo $JDATE |cut -c 3-7`

set iwest = ${IWEST_AQMS_Dx[$GRID]}
set ieast = ${IEAST_AQMS_Dx[$GRID]}
set jsouth = ${JSOUTH_AQMS_Dx[$GRID]}
set jnorth = ${JNORTH_AQMS_Dx[$GRID]}
set reso = $DOMAINS_RES
set RUN = HKPATH.${G_DOMAINS_RES[$GRID_COARSE]}
ln -fs $CAMxOUT/$RUN/CAMx5.4.$RUN.$YYYY$MM$DD.avrg.grd01 CAMx5.4.$RUN.$YYYY$MM$DD.avrg

./bndextr.linux << EOF >& ./msg/log.bndexr.${reso}.$JDATE
input average file |CAMx5.4.$RUN.$YYYY$MM$DD.avrg
output BC file     |$CAMxDir/bc.camx.${reso}km.$JDATE.uam
iwst,iest,jsth,jnth|$iwest,$ieast,$jsouth,$jnorth
mesh factor        |3
make IC file?      |$ic
time/date for IC   |${HH}  $dy
output IC file     |$CAMxDir/icon.camx.${reso}km.${BDATE}.bin
diagnostic file #1 |msg/camx.${reso}km.$JDATE.diag1
diagnostic file #2 |msg/camx.${reso}km.$JDATE.diag2
EOF


rm -f CAMx5.4.$RUN.*
